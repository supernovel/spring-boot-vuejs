plugins {
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    /**
         In-Memory database used for local development & testing
     */
    implementation 'com.h2database:h2:1.4.197'

    /**
        Switch back from Spring Boot 2.x standard HikariCP to Tomcat JDBC,
		configured later in Heroku (see https://stackoverflow.com/a/49970142/4964553)
    */
    implementation 'org.apache.tomcat:tomcat-jdbc:9.0.13'

    /**
        PostgreSQL used in Staging and Production environment, e.g. on Heroku
     */
    implementation 'org.postgresql:postgresql:42.2.5'

    /** 
        Testing 
    */   
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:3.2.0'
}

task copyResources(type: Copy, dependsOn: ':frontend:npmBuild') {
    from("${rootProject.rootDir}/frontend/target/dist") {
        include 'static/**'
        include 'index.html'
        include 'favicon.ico'
    }
    into("src/main/resources/public")
}

processResources.dependsOn 'copyResources'